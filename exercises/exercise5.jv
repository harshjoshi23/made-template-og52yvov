pipeline GTFSDataProcessingPipeline {

  DataDownloader -> ArchiveProcessor;
  ArchiveProcessor -> FileSelector -> TextProcessor -> CSVProcessor -> TableProcessor -> SQLiteWriter;

  block DataDownloader oftype HttpExtractor {
    url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
  }

  block ArchiveProcessor oftype ArchiveInterpreter {
    archiveType: "zip";
  }

  block FileSelector oftype FilePicker {
    path: "./stops.txt";
  }

  block TextProcessor oftype TextFileInterpreter {  
    encoding: "utf8"; 
  }

  block CSVProcessor oftype CSVInterpreter { 
    delimiter: ",";
    enclosing: '"';
  }

  valuetype ZoneIDType oftype integer {
    constraints: [ ValidateZoneId ];
  } 

  block TableProcessor oftype TableInterpreter {
    header: true;
    columns: [
      "stop_id" oftype integer, 
      "stop_name" oftype text, 
      "stop_lat" oftype GeoCoordType,
      "stop_lon" oftype GeoCoordType,
      "zone_id" oftype ZoneIDType
    ];
  }

  constraint CoordinateRange oftype RangeConstraint {
    lowerBound: -90;
    upperBound: 90;
    lowerBoundInclusive: true;
    upperBoundInclusive: true;
  }

  valuetype GeoCoordType oftype decimal {
    constraints: [CoordinateRange];
  }

  constraint ValidateZoneId on integer: value == 1925;

  block SQLiteWriter oftype SQLiteLoader {
    table: "stops";
    file: "./gtfs.sqlite";
  }
}
